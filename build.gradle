plugins {
    id "eclipse"
    id "net.minecraftforge.gradle" version "[6.0.16,6.2)"
    id "org.spongepowered.mixin" version "0.7.+"
    id "org.parchmentmc.librarian.forgegradle" version "1.+"
}

repositories {
    maven { url = "https://maven.minecraftforge.net/" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url = "https://cursemaven.com/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://maven.bawnorton.com/releases" }
    maven { url = "https://maven.enjarai.dev/mirrors" }

    flatDir { dirs "libs" }
}

version = "${modVersion}_${loaderType}-${minecraftVersion}"
group = "net.mysticcreations.true_end"
archivesBaseName = "TrueEnd"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: "parchment", version: "${parchmentVersion}"

    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    copyIdeResources = true

    runs {
        client {
        }

        server {
        }

        configureEach {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            mods {
            }
        }
    }
}

dependencies {
    //Dependencies
    minecraft "net.minecraftforge:forge:${forgeVersion}"
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${minecraftVersion}-${terrablenderVersion}")
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixinextrasVersion}"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:${mixinextrasVersion}")) {
        jarJar.ranged(it, "[${mixinextrasVersion},)")
    }
    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:${mixinsquaredVersion}"))
    implementation(jarJar("com.github.bawnorton.mixinsquared:mixinsquared-forge:${mixinsquaredVersion}")) {
        jarJar.ranged(it, "[${mixinsquaredVersion},)")
    }

    //Performance
    implementation fg.deobf("curse.maven:embeddium-908741:5681725")

    //APIs
    implementation fg.deobf("curse.maven:architectury-api-419699:5137938")
    implementation fg.deobf("me.shedaniel.cloth:cloth-config-forge:11.1.136")

    //Compatibility updates
    //Keep this in here
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:5.4.3+1.20.1")
    implementation fg.deobf("curse.maven:accessories-938917:6201361")
    implementation fg.deobf("curse.maven:nostalgic-tweaks-592465:6800425")
}

tasks.withType(ProcessResources).configureEach {
    inputs.property("modVersion", modVersion)
    inputs.property("minecraftVersion", minecraftVersion)
    inputs.property("terrablenderversion", terrablenderVersion)

    filesMatching("META-INF/mods.toml") {
        expand(
                modVersion: modVersion,
                minecraftVersion: minecraftVersion,
                terrablenderVersion: terrablenderVersion,
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

mixin {
    add sourceSets.main, "refmap.true_end.json"
    config "mixins.true_end.json"
}